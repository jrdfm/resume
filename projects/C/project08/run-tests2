#!/bin/tcsh

#
# (c) Larry Herman, 2021.  You are allowed to use this code yourself, but
# not to provide it to anyone else.
#

set all_correct
unset anyerror

foreach test (public? public?? secret? secret?? student? student??)

  if (-e $test.input) then
    $test < $test.input |& cmp -s - $test.output
    set exit_status = $status
  else
    if (-e $test.output) then
      $test |& cmp -s - $test.output
      set exit_status = $status
    else
      $test
      set exit_status = $status
    endif
  endif

  if ($exit_status == 0) then
    echo "Test $test passed\!"
  else
    echo "Test $test FAILED.  :-("
    unset all_correct
   endif

end

echo
if ($?all_correct) then
  echo "All tests passed\!"
else
  echo "Some tests failed. :-("
endif
